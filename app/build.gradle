apply plugin: 'com.android.application'
def keystorePropertiesFile = rootProject.file("private/keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
android {
    signingConfigs {
        config {
            keyAlias 'upload'
            keyPassword 'mohtabcd1234'
            storeFile file('../keystores/beiwe-moht.jks')
            storePassword 'moht1234abcd'
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.0 rc2'
    defaultConfig {
        applicationId "org.beiwe.app"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 33       //TODO: update this Beiwe version code for new releases
        versionName '2.4.4'  //TODO: update this Beiwe version number for new releases
        setProperty("archivesBaseName", "Beiwe-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            manifestPlaceholders = [appNameSuffix: ""]
            buildConfigField("boolean", "APP_IS_BETA", "false")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['releaseDSN'] + "\"")
            signingConfig signingConfigs.config
        }
        beta {
            versionNameSuffix("-beta")
            manifestPlaceholders = [appNameSuffix: "-beta"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['betaDSN'] + "\"")
            signingConfig signingConfigs.config
        }
        development {
            versionNameSuffix("-development")
            manifestPlaceholders = [appNameSuffix: "-development"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "true")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['developmentDSN'] + "\"")
            debuggable true
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions "customizableServerUrl"
    productFlavors {
        googlePlayStore {
            dimension "customizableServerUrl"
            manifestPlaceholders = [appName: "Beiwe2"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", 'true')
        }
        onnelaLabServer {
            dimension "customizableServerUrl"
            manifestPlaceholders = [appName: "Beiwe"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "false")
        }
    }
    useLibrary "org.apache.http.legacy"
}
// Don't include the default "debug" build variant as an option
android.variantFilter { variant ->
    if (variant.buildType.name.equals('debug')) {
        variant.setIgnore(true);
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.commonsware.cwac:anddown:0.3.0'
    implementation 'io.sentry:sentry-android:1.7.23'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation "com.google.android.gms:play-services-vision:11.8.0"
}
